buildscript {
  ext.kotlin_version = '1.2.10'
  ext.kotlin_xml_dsl_version = '0.1.6'

  repositories { mavenCentral() }
  dependencies { classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" }
}

plugins {
  id "com.jfrog.bintray" version "1.5"
}

repositories {
  mavenCentral()
}

version = project.hasProperty('teamcity') ? project['teamcity']['build.number'] : 'SNAPSHOT'

subprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'

  group = 'org.jonnyzzz.kotlin.xml.bind'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jonnyzzz/maven" }
  }

  dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "junit:junit:4.12"
  }

  test {
    useJUnit()
    testLogging.showStandardStreams = true
  }


  apply plugin: 'maven-publish'
  apply plugin: "com.jfrog.bintray"

  task sourceJar(type: Jar) {
    from sourceSets.main.allSource
  }

  artifacts {
    archives sourceJar
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        artifact sourceJar {
          classifier "sources"
        }
      }
    }
  }

  if (rootProject.hasProperty("bintrayUser")) {
    bintray {
      user = getProperty('bintrayUser')
      key = getProperty('bintrayApiKey')
      publications = ['mavenJava']
      pkg {
        repo = 'maven'
        name = 'kotlin.xml.bind'
        userOrg = getProperty('bintrayUser')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jonnyzzz/kotlin.xml.bind.git'
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}

task bintrayUploadEx {
  dependsOn ':api:bintrayUpload'
  dependsOn ':jdom:bintrayUpload'
}

task localUploadEx {
  dependsOn ':api:publishToLocalMaven'
  dependsOn ':jdom:publishToLocalMaven'
}
